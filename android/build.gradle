// The Android Gradle Plugin builds the native code with the Android NDK.

group 'com.gtbluesky.onnxruntime'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        // The Android Gradle Plugin knows how to build native code with the NDK.
        classpath 'com.android.tools.build:gradle:7.3.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'com.gtbluesky.onnxruntime'
    }

    // Bumping the plugin compileSdkVersion requires all clients of this plugin
    // to bump the version in their app.
    compileSdkVersion 35

    // Use the NDK version
    // declared in /android/app/build.gradle file of the Flutter project.
    // Replace it with a version number if this plugin requires a specfic NDK version.
    // (e.g. ndkVersion "23.1.7779620")
    ndkVersion android.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 21
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a'
        }
    }

    // 使用预编译的库，但会在构建后重新对齐
    sourceSets {
        main {
            jniLibs {
                srcDirs = ["src/main/jniLibs"]
            }
        }
    }
}

tasks.register("buildOnnxruntime16kArm64", Exec) {
    workingDir projectDir
    commandLine "bash", "build_onnxruntime_16kb.sh", "arm64-v8a"
}

tasks.register("buildOnnxruntime16kArmeabi", Exec) {
    workingDir projectDir
    commandLine "bash", "build_onnxruntime_16kb.sh", "armeabi-v7a"
}

tasks.register("buildOnnxruntime16kAll") {
    dependsOn("buildOnnxruntime16kArm64", "buildOnnxruntime16kArmeabi")
}
